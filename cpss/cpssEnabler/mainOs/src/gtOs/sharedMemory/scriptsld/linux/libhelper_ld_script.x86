/* ldscript file to be used by ld for libhelper.so DSO for i386 architecture.
*/
SECTIONS
{
/* The only purpose of this script to place ALL code and data of helper library
on fixed well-known addresses. In this way every client will have helper code/data
on the same addresses, so libcpss library can safely refer to libhelper library inside
different clients.

Otherwise dynamic linker can place helper library on different addesses for different clients,
so libcpss cannot reefer correctly to the libhelper library in the same manner for all clients.

*/
. = 0x14600000;
.hash : { *(.hash) }
.dynsym : { *(.dynsym) }
.dynstr : { *(.dynstr) }
.gnu.version : { *(.gnu.version) }
.gnu.version_r : { *(.gnu.version_r) }
.rel.dyn : { *(.rel.dyn) }
.rel.plt : { *(.rel.plt) }
.init : { *(.init) }
.plt : { *(.plt) }
.text : { *(.text) }
.fini : { *(.fini) }
.rodata : { *(.rodata) }
.eh_frame : { *(.eh_frame) }
.ctors : { *(.ctors) }
.dtors : { *(.dtors) }
.jcr : { *(.jcr) }
.dynamic : { *(.dynamic) }
.got : { *(.got) }
.got.plt : { *(.got.plt) }
.data : { *(.data) }
.bss : { *(.bss) }

/* Shared data section, access rights RW.
   This section contain BSS and DATA sections of mainOS library which are located in special
   section "MAINOS". The __SHARED_DATA_MAINOS attribute should be used to mark data.
   This section is shared for all clients. Shared Library initialization routines find this
   section by begin address and remap it into the shared buffer.
*/
MAINOS 0x14680000 : { *(MAINOS) }

/* Reserve addresses till 0x40000000
   This reserved addresses include
   0x14800000 .. 0x1c800000   - CPSS_SHM_MALLOC (128M)
   0x1c800000 .. 0x1cc00000   - DMA'ble memory (4M)
   0x1cc00000 .. 0x1e000000   - PP conf (20M)
   0x1e000000 .. 0x20000000   - PP dfx  (32M)
   0x20000000 .. 0x40000000   - PP regs (512M == 8*64M)
*/
RESERVEADDRS 0x3ffff000 (NOLOAD) : { LONG(0); } /* one page to 0x40000000 */

}
